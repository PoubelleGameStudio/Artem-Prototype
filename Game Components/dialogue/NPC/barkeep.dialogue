~ start
if quest_db["barkeep_1"]["Status"] == 0:
	if State.welcomed == true:
		=> Barkeep_welcome
	else
		=> no_welcome
elif quest_db["barkeep_1"]["Status"] == 1:
	=> in_prog
elif quest_db["barkeep_1"]["Status"] == 2 and quest_db["barkeep_2"]["Status"] == 0:
	=> next
elif quest_db["barkeep_2"]["Status"] == 1:
	=> next_prog
elif quest_db["barkeep_3"]["Status"] == 0:
	=> tim
elif quest_db["barkeep_3"]["Status"] == 1:
	=> tim_progress
elif quest_db["barkeep_3"]["Status"] == 2:
	=> all_done



~ Barkeep_welcome
Mayor Barkeep: Hello there. Haven't had any newcomers in a while now.
Artem: Is this Grim's Briar? I was given directions by a very drunk man.
Mayor Barkeep: Sounds like you met Hedo out on the trails, but he did get you to the right place. I'll guess based on your robes that you're here about your buddy?
Artem: Co-worker more than friend, really, but yes. What can you tell me?
=> main_path
	
~ no_welcome	
Mayor Barkeep: Would ya look at that! A new face in town! What can we do ya for?
Artem: Hi, I'm Artem, a member of the Eye. By any chance am I actually in or close to Grim's Briar?
Mayor Barkeep: Not sure you could get any closer. We don't tend to draw folks with a purpose, though. How'd you find yourself here?
Artem: It would appear a co-worker of mine went missing in the last week or two. Have you seen any other Watchers pass through?
=> main_path
	
~ main_path
Mayor Barkeep: You may notice how quiet things are around town. A week ago we had a bit of an incident.
Artem: I'm all ears.
Mayor Barkeep: All but myself, hedo, and a couple workers vanished overnight. Your friend was one of them.
Artem: Maybe they all suddenly gained a sense of purpose?
Mayor Barkeep: Nothing that drastic. We don't know how exactly he managed it, but we know Tim was behind it. He's a local writer, more or less.
Artem: What happened? Bad review?
Mayor Barkeep: I realize that was supposed to be calloused sarcasm, but yes. The local art rag wasn't a fan of his recent novel. He got about as upset as I've ever seen. Not long after that, everyone he gave a copy to went missing. Just all at once.
Artem: Did Tim go with them?
Mayor Barkeep: Maybe? I've been trying to track him down. If not to set him straight then to at least find out where he sent everyone.
Artem: Well, consider me now at your disposal. Let me see everything you've got so far.
Mayor Barkeep: I appreciate your help, and I apologize for how much we need it. Talk to Thorn for a drink and check in with the Old Crusty Guy at the inn. I'll gather what little info I have and you can meet me back here once you've made the rounds.
do update_quest_status("barkeep_1",1)
=> END

~ in_prog
if quest_complete("barkeep_1") == false:
	Mayor Barkeep: You can get a drink in the northwest side of town. For a bed you'll need to head to the north east side.
	set talking = 0
	=> END
else:
	Mayor Barkeep: Hope you settled in well enough. Here's a little something from the tourism grant. Make sure to shop local!
	set gold += 10
	set cur_xp += 10
	do level_up()
	do update_quest_status("barkeep_1",2)
	do update_quest_status("barkeep_2",0)
	set talking = 0
	=> END

~ next
Artem: Your architecture is deeply unorthodox
Mayor Barkeep: It's a bit eclectic I suppose. Renovations are done by whoever is around and steady enough, so we get a bit of everyone's influence.
Artem: And they just stay perpetually suspended like that?
Mayor Barkeep: What?
Artem: What?
Mayor Barkeep: Oh, right...the air here is psychoactive. Locals develop a tolerance to it for the most part, but we do keep some of those rad plague doctor masks. Those...probably help?
Artem: I'm being drugged?
Mayor Barkeep: Okay, it's totally fine. You're going to be absolutley totally fine for sure. No doubt.
Mayor Barkeep: ...However, you also have some time before you peak. The crusty guy at the inn knows how to make what we call the "Get Me Down Potion".
Artem: And that will make it so [wave amp=25 freq=5]hallucinations[/wave] are not a side effect of breathing?
Mayor Barkeep: Almost always.
Artem: Then off I go.
do update_quest_status("barkeep_2",1)
set talking = 0
=> END


~ next_prog
if quest_complete("barkeep_2") == false:
	Mayor Barkeep: You're gonna be just fine, but you should probably start gathering those ingredients immediately.
	set talking = 0
	=> END
else:
	Mayor Barkeep: I knew you'd sort it all out! Had zero doubts about that since we last spoke. Highly confident.
	Artem: With my challenged sobriety sorted. Let's get to work on the Tim situation.
	do update_quest_status("barkeep_2",2)
	set talking = 0
	=> END

~ tim
Mayor Barkeep: I've been pouring through his recent works, and to be honest I'm with the critic on this one, but nothing in the themes stands out. 
Artem: Don't see a lot of houses around. Where'd he sleep? Maybe he left some notes lying around...
Mayor Barkeep: Most of the locals spend their days at the Tavern and shamble over to the Inn once they're out of ink. 
Mayor Barkeep: Thorn's mentioned they saw him taking some late night strolls recently, which is odd given no one here cares much for their health..
Artem: Did Thorne mention where exactly he was walking towards?
Mayor Barkeep: We stay pretty busy slinging drinks, but they noticed him heading towards the cemetary south of here.
Artem: Hiding out in a mausoleum maybe?
Mayor Barkeep: You're really good at this. You know that?
Artem: Painfully aware...
do update_quest_status("barkeep_3",1)
=> END

~ tim_progress
if quest_complete("barkeep_3") == false:
	Mayor Barkeep: Stay safe out there. Hard to know who and what to trust right now.
else:
	Mayor Barkeep: You found him?
	Artem: What was left of him at least. He found a cave in the forest to hide out in. Based the journal he left behind it sounds like he never planned on leaving it. 
	Mayor Barkeep: Jinkies...what compelled him to choose this?
	Artem: He was working, at least in some capacity, with the technotheists. 
	Mayor Barkeep: Are you sure?
	Artem: The Witch in the forest confirmed the source of the magic on the book and his writing removed any doubt. While it's true they gave him the curses that stole away the townies and inadvertantly brought me here, he wasn't fully aware of what he was doing. They told him the books would be charmed to make anyone who read it think it was brilliant.
	Mayor Barkeep: So, he was right that the critic didn't even read the book...
	Artem: It would appear so, but that brings me to the townies. The Needle has been constructing what they're calling energy temples, but they're nothing more than glorified torture chambers. Unfortunately, that's where the books took them.
	Artem: Once he realized what he'd done, he couldn't live with the guilt and let himself waste away alone in that cave.
	Mayor Barkeep: Those bastards. They have to be held accountable for what they've done. For what they're doing...
	Artem: They will be. I had a long talk with The Witch in the forest and she helped me to destroy the temple. This trip had far more revelations that I ever would have expected for a dreary artist's village hidden away in the mountains.
	Mayor Barkeep: I can't think you enough for your help with everything. Here, take this runestone. Speak the location you wish to be, and break the stone under your foot. Don't worry, it's not sturdy as it looks, but know it's a one way trip.
	Artem: Thank you. I can't say it's been a relaxing or very enjoyable time, but it's clear I was meant to find myself here. I hope you never need my help again, but don't hestiate if you do.
	do update_quest_status("barkeep_3",2)
	do SteamFeatures.setAchievement("ACH_Mayor")

~ all_done
Mayor Barkeep: You're welcome back anytime, but there's not much for you right now.
=> END